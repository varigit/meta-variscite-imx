#@TYPE: Machine
#@NAME: Variscite VAR-SOM-MX6
#@SOC: i.MX6Q/D/DL/S
#@DESCRIPTION: Machine configuration for Variscite VAR-SOM-MX6 VAR-SOM-SOLO/DUAL DART-MX6
#@MAINTAINER: Eran Matityahu <eran.m@variscite.com>
#
# http://www.variscite.com

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa9.inc

require variscite-mirrors.inc

SOC_FAMILY = "mx6:mx6q:mx6dl"

SERIAL_CONSOLE = "115200 ttymxc0"

MACHINE_FEATURES += " pci bluetooth touchscreen wifi"

# UBIFS for Variscite NAND 0.5 GB
MKUBIFS_ARGS = " -m 2048 -e 124KiB -c 3965 "
UBINIZE_ARGS = " -m 2048 -p 128KiB -s 2048 -O 2048 "

# UBIFS for Variscite NAND 1.0 GB (models with erase block size of 128KiB)
#MKUBIFS_ARGS = " -m 2048 -e 124KiB -c 7855 "
#UBINIZE_ARGS = " -m 2048 -p 128KiB -s 2048 -O 2048 "

# UBIFS for Variscite NAND 1.0 GB (models with erase block size of 512KiB)
#MKUBIFS_ARGS = " -m 4096 -e 504KiB -c 2000 "
#UBINIZE_ARGS = " -m 4096 -p 512KiB -s 4096 -O 4096 "

UBI_VOLNAME  = "rootfs"

# Variscite BSP default providers
PREFERRED_PROVIDER_virtual/kernel_var-som-mx6 ?= "linux-variscite"
PREFERRED_VERSION_linux-variscite = "4.1.15"
KERNEL_DEVICETREE = " \
		     imx6dl-var-som-cap.dtb \
		     imx6dl-var-som-res.dtb \
		     imx6dl-var-som-solo-cap.dtb \
		     imx6dl-var-som-solo-res.dtb \
		     imx6dl-var-som-solo-vsc.dtb \
		     imx6dl-var-som-vsc.dtb \
		     imx6q-var-dart.dtb \
		     imx6q-var-som-cap.dtb \
		     imx6q-var-som-res.dtb \
		     imx6q-var-som-vsc.dtb \
		     "
KERNEL_IMAGETYPE = "uImage"

PREFERRED_PROVIDER_wpa-supplicant-cli = "wpa-supplicant-wl18xx"
PREFERRED_PROVIDER_wpa-supplicant-passphrase = "wpa-supplicant-wl18xx"
PREFERRED_PROVIDER_wpa-supplicant = "wpa-supplicant-wl18xx"

PREFERRED_PROVIDER_u-boot_var-som-mx6 = "u-boot-variscite"
PREFERRED_PROVIDER_virtual/bootloader_var-som-mx6 = "u-boot-variscite"
UBOOT_MAKE_TARGET = ""
SPL_BINARY          = "SPL"
UBOOT_SUFFIX        = "img"
UBOOT_CONFIG ??= "nand sd "
# The above order matters. The last one will be also called u-boot-${MACHINE} and will be used to create the sdcard.
UBOOT_CONFIG[sd] = "mx6var_som_sd_config,sdcard"
UBOOT_CONFIG[nand] = "mx6var_som_nand_config,ubifs"

MACHINE_EXTRA_RDEPENDS += " \
			   wlconf \
			   iw \
			   hostap-daemon-wl18xx \
			   libnl \
			   crda \
			   wl18xx-target-scripts \
			   ti-compat-wireless-wl18xx \
			   ti-wifi-utils \
			   wpa-supplicant-wl18xx \
			   bluez5 \
			   wl18xx-firmware \
			   u-boot-fw-utils \
			   u-boot-splash \
			   ${@bb.utils.contains('DISTRO_FEATURES', 'x11', "x11vnc", "", d)} \
			   ${@bb.utils.contains('DISTRO_FEATURES', 'x11', "gst-player-bin", "", d)} \
			   "
# Packages added to all images (including core-image-minimal)
IMAGE_INSTALL_append = " \
        fsl-rc-local \
"

BBMASK_prepend = "fsl-image-mfgtool-initramfs "

IMAGE_FSTYPES_var-som-mx6 ?= "tar.bz2 ext4 sdcard ubi"
